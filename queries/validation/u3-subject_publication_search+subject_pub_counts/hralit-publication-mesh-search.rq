PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX uberon: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX cl: <http://purl.obolibrary.org/obo/CL_>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX HRAlit: <https://purl.humanatlas.io/graph/hra-lit>
PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?pmid
       ?title 
       ?datePublished 
       ?publicationYear
       ?journal_title 
	     (SAMPLE(COALESCE(?abs, " ")) AS ?abstact)
       (GROUP_CONCAT(DISTINCT ?authorName; SEPARATOR="|") AS ?authors)
       (SAMPLE(COALESCE(?volNum, " ")) AS ?vol)
       (SAMPLE(COALESCE(?issNum, " ")) AS ?iss)
       (GROUP_CONCAT(DISTINCT ?subjects; SEPARATOR="|") AS ?subjectHeadings)
       (SAMPLE(COALESCE(?doc_id, " ")) AS ?doi)
FROM HRAlit:
FROM NAMED <http://id.nlm.nih.gov/mesh/>
WHERE {
  # hint:Query hint:analytic "true" .
  # VALUES (?descriptor) {
  #   (mesh:D006325)
  # }
  # Article metadata
	?article schema:about ?descriptor ;
           a schema:ScholarlyArticle ;
           schema:identifier ?pmid ;
           schema:name ?title ;
           schema:datePublished ?datePublished ;
           schema:about ?desc ;
           schema:author ?author ;
           schema:isPartOf ?journal .
  # Extract publication year from datePublished
   BIND(
      IF(CONTAINS(?datePublished, "-"),
         STRBEFORE(?datePublished, "-"),
         IF(STRLEN(STR(?datePublished)) >= 4,
            SUBSTR(STR(?datePublished), 1, 4),
            STR(?datePublished)
         )
      ) AS ?publicationYear
    )   
  # Author names
  ?author schema:name ?authorName .
  # Journal Title
  ?journal a schema:Periodical ;
  schema:name ?journal_title .
  # Mesh Labels
  GRAPH <http://id.nlm.nih.gov/mesh/> {
    ?desc rdfs:label ?subjects .
  }
  OPTIONAL {
    ?article schema:abstract ?abs ;
             schema:sameAs ?doc_id ;
             schema:isPartOf ?volume ;
             schema:isPartOf ?issue .
    ?volume a schema:PublicationVolume ;
            schema:volumeNumber ?volNum .
    ?issue a schema:PublicationIssue ;
            schema:issueNumber ?issNum .
  }

  # Skeleton Cell
  FILTER(?descriptor = mesh:D012863)
}

GROUP BY ?pmid ?datePublished ?publicationYear ?title ?journal_title 
ORDER BY DESC(?datePublished)
# LIMIT 200