PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX uberon: <http://purl.obolibrary.org/obo/uberon#>
PREFIX cl: <http://purl.obolibrary.org/obo/cl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX HRAlit: <https://purl.humanatlas.io/graph/hra-lit>
PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?descriptor
       ?descriptor_label
       ?journal_title 
       ?year
       (COUNT(?pmid) AS ?publications)

FROM HRAlit:
FROM NAMED <http://id.nlm.nih.gov/mesh/>
WHERE {
   # Article metadata
	 ?article schema:about ?descriptor ; # Key for filter.
            schema:isPartOf ?journal ; # Key for filter ?journal
            a schema:ScholarlyArticle ;
            schema:identifier ?pmid ;
            schema:datePublished ?datePublished .

   # Extract publication year from datePublished
   BIND(xsd:integer(
            IF(CONTAINS(?datePublished, "-"),
            STRBEFORE(?datePublished, "-"),
            IF(STRLEN(STR(?datePublished)) >= 4,
                SUBSTR(STR(?datePublished), 1, 4),
                STR(?datePublished)
                )
            )  
        ) AS ?year
    )   
  
  # Journal Title (Publishes papers on skeletons)
  ?journal a schema:Periodical ;
           schema:name ?journal_title . # Key for filter ?journal_title
  FILTER(?journal_title = "American journal of physical anthropology")

  # Skeleton
  GRAPH <http://id.nlm.nih.gov/mesh/> {
     ?descriptor rdfs:label ?descriptor_label .
  }
  FILTER(?descriptor = mesh:D012863)
}

GROUP BY ?journal_title ?descriptor ?descriptor_label ?year
ORDER BY ?journal_title DESC(?year)